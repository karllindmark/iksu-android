apply plugin: 'com.android.application'

project.ext.iksu = [
    apiUsername: '',
    apiPassword: '',
    apiGrant: '',

    accountEmail: '',
    accountPassword: '',

    keystorePath: '',
    keystoreItemAlias: '',
    keystoreItemPassword: ''
]
loadExtraProperties("credentials.properties")

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.0"
    defaultConfig {
        applicationId "com.ninetwozero.iksu"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 11
        versionName "1.0.3 (ABDOMINAL)"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_USER", '"' + project.iksu['apiUsername'] + '"'
        buildConfigField "String", "API_PASSWORD", '"' + project.iksu['apiPassword'] + '"'
        buildConfigField "String", "API_GRANT", '"' + project.iksu['apiGrant'] + '"'

        buildConfigField "String", "ACCOUNT_EMAIL", '"' + project.iksu['accountEmail'] + '"'
        buildConfigField "String", "ACCOUNT_PASSWORD", '"' + project.iksu['accountPassword'] + '"'
    }

    signingConfigs {
        defaultConfig {
            storeFile file(project.iksu['keystorePath'])
            storePassword = project.iksu['keystorePassword']
            keyAlias = project.iksu['keystoreItemAlias']
            keyPassword = project.iksu['keystoreItemPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.defaultConfig

            buildConfigField "String", "ACCOUNT_EMAIL", '""'
            buildConfigField "String", "ACCOUNT_PASSWORD", '""'
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:27.0.0'
    compile 'com.android.support:design:27.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:preference-v14:27.0.0'

    compile 'com.afollestad.material-dialogs:core:0.9.4.3'
    compile 'de.hdodenhof:circleimageview:2.1.0'

    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.3.0'
    compile 'com.squareup.moshi:moshi:1.5.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.16'
    compile 'io.realm:android-adapters:2.0.0'

    // Crash reporting and event logging
    compile 'com.google.firebase:firebase-core:10.2.4'
    compile 'com.google.firebase:firebase-crash:10.2.4'


    testCompile 'junit:junit:4.12'
}

// https://stackoverflow.com/a/30105276/860212
def loadExtraProperties(String fileName) {
    String loadFile;
    def props = new Properties()
    if (new File(fileName).exists()) {
        loadFile = fileName
    } else {
        loadFile = "credentials.properties.sample"
    }
        props.load(new FileInputStream(loadFile))
        props.each { key, val ->
            project.ext.iksu[key] = val
        }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'realm-android'
